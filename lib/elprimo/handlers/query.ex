defmodule Elprimo.Handlers.Query do
  @moduledoc """
  Handle all income Telegram updates which touch a query generation.
  """

  use Telegex.Chain

  import Elprimo.Utils

  require Logger

  alias Telegex.Type.InlineKeyboardButton
  alias Telegex.Type.InlineKeyboardMarkup
  alias Telegex.Type.Update
  alias Elprimo.State

  @command "query"

  def label() do
    "Ð—Ð°Ð¿Ñ€Ð¾Ñ Ð½Ð° Ð”Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚Ñ‹ ðŸ“ƒ"
  end

  @impl Telegex.Chain
  def match?(%Update{message: msg} = _upd, _context) do
    cond do
      msg && msg.from && msg.text && check_command(msg.text, @command) ->
        true

      msg && msg.from && msg.text && msg.text == label() ->
        true

      msg && msg.from ->
        state = State.get(msg.from.id)
        Kernel.match?({:query_field, _, _, _}, state) || state == :query_type

      true ->
        false
    end
  end

  @impl Telegex.Chain
  def handle(%Update{message: msg} = _upd, context) do
    user = Elprimo.User.by_telegram_id(msg.from.id)
    text = msg.text
    state = State.get(user.telegram)
    next_state(state, user, text)
    {:done, context}
  end

  @spec next_state(State.t(), Elprimo.User.t(), String.t()) :: any()
  def next_state(state, %Elprimo.User{} = user, text) do
    case state do
      :none ->
        kb = %InlineKeyboardMarkup{
          inline_keyboard: [
            [%InlineKeyboardButton{text: "Ð’Ñ‹Ð±Ñ€Ð°Ñ‚ÑŒ", switch_inline_query_current_chat: ""}]
          ]
        }

        Telegex.send_message(user.telegram, "Ð—Ð°Ð¿Ñ€Ð¾Ñ Ð½Ð° ÐºÐ°ÐºÐ¾Ð¹ Ð´Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚ Ñ…Ð¾Ñ‚Ð¸Ñ‚Ðµ Ð¿Ð¾Ð´Ð°Ð²Ð°Ñ‚ÑŒ?",
          reply_markup: kb
        )

        State.update(user.telegram, :query_type)

      :query_type ->
        doctype = Elprimo.Doctype.by_name(text)

        if doctype == nil do
          Telegex.send_message(
            user.telegram,
            "ÐÐµÐ¿Ñ€Ð°Ð²Ð¸Ð»ÑŒÐ½Ñ‹Ð¹ Ñ‚Ð¸Ð¿ Ð´Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚Ð°, Ð¿Ð¾Ð¿Ñ€Ð¾Ð±ÑƒÐ¹Ñ‚Ðµ ÐµÑ‰Ñ‘ Ñ€Ð°Ð·!  Ð•ÑÐ»Ð¸ Ñ‡Ñ‚Ð¾ Ð²Ñ‹ Ð¼Ð¾Ð¶ÐµÑ‚Ðµ ÑƒÐ²Ð¸Ð´ÐµÑ‚ÑŒ ÑÐ¿Ð¸ÑÐ¾Ðº Ð²Ð¾Ð·Ð¼Ð¾Ð¶Ð½Ñ‹Ñ… Ð´Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚Ð¾Ð², Ð½Ð°Ð¶Ð°Ð² ÐºÐ½Ð¾Ð¿ÐºÑƒ \"Ð’Ñ‹Ð±Ñ€Ð°Ñ‚ÑŒ\" ÑÐ²ÐµÑ€Ñ…Ñƒ.  Ð•Ñ‰Ñ‘ Ð²Ð¾Ð·Ð¼Ð¾Ð¶Ð½Ð¾ Ð²Ð°Ð¼ Ð½ÐµÐ¾Ð±Ñ…Ð¾Ð´Ð¸Ð¼Ð¾ Ð¾Ñ‚Ð¼ÐµÐ½Ð¸Ñ‚ÑŒ Ñ‚ÐµÐºÑƒÑ‰ÑƒÑŽ Ð¾Ð¿ÐµÑ€Ð°Ñ†Ð¸ÑŽ, Ð½Ð°Ð¶Ð°Ð² /cancel"
          )
        else
          State.update(user.telegram, {:query_field, 2, doctype.id, ""})
          next_field_question(user, 1, doctype.id)
        end

      {:query_field, number, doctype_id, info} ->
        # so text is answer on previous field question
        State.update(
          user.telegram,
          {:query_field, number + 1, doctype_id, info <> Elprimo.Query.separator() <> text}
        )

        next_field_question(user, number, doctype_id)

      _ ->
        need_cancel(user)
    end
  end

  @spec next_field_question(Elprimo.User.t(), integer(), integer()) :: any()
  def next_field_question(user, number, doctype_id) do
    a = ask_field_question(user, number, doctype_id)

    unless a do
      stop_gen_query(user)
    end
  end

  @doc """
  Ask field question with the given number the user.

  If next questions are exists return true, otherwise false.
  """
  @spec ask_field_question(Elprimo.User.t(), integer(), integer()) :: boolean()
  def ask_field_question(user, number, doctype_id) do
    field = Elprimo.Field.find(doctype_id, number)

    if field do
      Telegex.send_message(user.telegram, field.prompt)
      true
    else
      false
    end
  end

  def stop_gen_query(%Elprimo.User{} = user) do
    state = State.get(user.telegram)

    with {:query_field, _number, doctype_id, info} <- state do
      q = %Elprimo.Query{doctype: doctype_id, info: info}
      Elprimo.Query.save_and_send_to_admins(q, user)

      Telegex.send_message(
        user.telegram,
        "Ð’Ð°ÑˆÐ° Ð·Ð°ÑÐ²ÐºÐ° Ð½Ð° Ð¿Ð¾Ð»ÑƒÑ‡ÐµÐ½Ð¸Ñ Ð´Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚Ð° Ð¿Ð¾Ð»ÑƒÑ‡ÐµÐ½Ð°, Ð¿Ñ€Ð¸Ñ…Ð¾Ð´Ð¸Ñ‚Ðµ Ð² ÑˆÐºÐ¾Ð»Ñƒ Ð² Ñ€Ð°Ð±Ð¾Ñ‡Ð¸Ðµ Ñ‡Ð°ÑÑ‹ Ð¿Ñ€Ð¸Ñ‘Ð¼Ð½Ð¾Ð¹"
      )

      State.update(user.telegram, :none)
    else
      _ -> Logger.error("We can't call `stop_gen_query` when state isn't :query_field")
    end
  end
end
